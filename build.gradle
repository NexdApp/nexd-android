// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.71'
    ext.nav_version = "2.2.1"
    ext.koin_version = '2.1.5'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
        classpath "org.koin:koin-gradle-plugin:$koin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "org.openapi.generator" version "4.3.0"
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

ext {
    apiBaseUrl = project.findProperty('apiBaseUrl') ?: "https://nexd-backend-staging.herokuapp.com/api/v1"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task removeApiSrc(type: Delete) {
    delete project(":api").file('src')
}

openApiValidate {
    inputSpec = "$apiBaseUrl/docs-json".toString()
}

task generateApiClient(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "java"
    inputSpec = "${rootProject.ext.apiBaseUrl}/docs-json"
    outputDir = "api"
    apiPackage = "app.nexd.android.api"
    invokerPackage = "app.nexd.android"
    modelPackage = "app.nexd.android.api.model"
    typeMappings = [
            "integer" : "long",
            "number" : "long"
    ]
    library = "retrofit2"
    generateModelTests = false
    generateApiTests = false
    configOptions = [
            useRxJava2             : "true",
            dateLibrary            : "legacy",
            hideGenerationTimestamp: "true"
    ]
    systemProperties = [
            modelDocs: "false"
    ]
}

generateApiClient.dependsOn removeApiSrc