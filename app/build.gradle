plugins {
    id 'com.android.application'
    id 'com.github.triplet.play' version '2.7.3'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'kotlin-kapt'
apply plugin: 'koin'

def buildVersionCode = project.hasProperty('versionCode') ? versionCode.toInteger() : 1
println "VersionCode is set to $buildVersionCode"

String signingKeystore = project.findProperty('keystore')
String signingKeystorePassword = project.findProperty('keystorePassword')
String signingKeyAlias = project.findProperty('keyAlias')
String signingKeyPassword = project.findProperty('keyPassword')

android {

    signingConfigs {
        release {
            storeFile signingKeystore != null ? file(project.rootProject.file(signingKeystore)) : null
            storePassword signingKeystorePassword
            keyAlias = signingKeyAlias
            keyPassword signingKeyPassword
        }
    }

    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "app.nexd.android"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode buildVersionCode
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "API_BASE_URL", "\"${rootProject.ext.apiBaseUrl}\"")
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            manifestPlaceholders = [APP_NAME: "nexd"]

            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            manifestPlaceholders = [APP_NAME: "nexd Debug"]

            debuggable true
            applicationIdSuffix = ".debug"
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    dataBinding {
        enabled = true
    }
}

play {
    serviceAccountCredentials = project.rootProject.file('publisher-key.json')
}

dependencies {
    implementation project(':api')

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.2'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation "androidx.lifecycle:lifecycle-reactivestreams:2.2.0"

    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"

    implementation "com.squareup.retrofit2:retrofit:2.8.1"
    implementation "com.squareup.retrofit2:converter-scalars:2.8.1"
    implementation "com.squareup.retrofit2:converter-gson:2.8.1"
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.8.1'

    implementation 'com.squareup.okhttp3:logging-interceptor:4.4.1'

    implementation 'com.itkacher.okhttpprofiler:okhttpprofiler:1.0.7'

    implementation 'dev.ahamed.mva2:adapter:2.0.0'
    implementation 'dev.ahamed.mva2:ext-databinding:2.0.0'
    implementation 'dev.ahamed.mva2:ext-decorator:2.0.0'
    implementation 'dev.ahamed.mva2:ext-diffutil-rx:2.0.0'

    implementation 'com.google.android.exoplayer:exoplayer-core:2.11.4'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.11.4'

    implementation 'com.google.android:flexbox:2.0.1'

    // Koin for Android
    implementation "org.koin:koin-android:$koin_version"
    // Koin AndroidX Scope features
    implementation "org.koin:koin-androidx-scope:$koin_version"
    // Koin AndroidX ViewModel features
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    // Koin AndroidX Fragment features
    implementation "org.koin:koin-androidx-fragment:$koin_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    testImplementation 'junit:junit:4.13'


    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
